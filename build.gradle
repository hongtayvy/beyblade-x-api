plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.10'
	id 'io.spring.dependency-management' version '1.1.7'
	// Optional: Jib for container builds
	id 'com.google.cloud.tools.jib' version '3.5.3'
}

group = 'com.hongtayvy.beybladex'
version = '0.0.1-SNAPSHOT'
description = 'Beyblade X API'

java {
	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

repositories {
	mavenCentral()
}

// Override Spring Boot managed version to fix CVE-2025-48924
// (Bootâ€™s dependency management was forcing commons-lang3 to 3.17.0)
dependencyManagement {
	dependencies {
		dependency 'org.apache.commons:commons-lang3:3.18.0'
	}
}

dependencies {
	// Core
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'com.fasterxml.jackson.core:jackson-databind'

	//Swagger
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15'
	// --- Security overrides for transitive deps ---
	// Keep Swagger UI webjar current (pulled transitively by springdoc)
	constraints {
		add('implementation', 'org.webjars:swagger-ui:5.31.0') {
			because 'Bring swagger-ui webjar to a current, patched version'
		}
	}

	// Database
	runtimeOnly 'com.h2database:h2'

	// Dev tools
	developmentOnly 'org.springframework.boot:spring-boot-devtools'

	// Lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// Testing
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'



}

configurations.configureEach {
	resolutionStrategy {
		force 'org.webjars:swagger-ui:5.31.0'
	}
}

tasks.withType(Test) {
	useJUnitPlatform()
}

bootJar {
	archiveFileName = 'beyblade-x-api.jar'
}

jib {
	from {
		image = 'eclipse-temurin:21-jre'
	}
	to {
		image = "hongtayvy/beyblade-x-api:${version}"
	}
	container {
		mainClass = 'com.hongtayvy.beybladex.BeybladeApiApplication'
		ports = ['8080']
		jvmFlags = ['-Djava.security.egd=file:/dev/./urandom']
	}
}